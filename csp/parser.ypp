/* 
   Copyright (c) 2011-2022 Andreas F. Borchert
   All rights reserved.

   Permission is hereby granted, free of charge, to any person obtaining
   a copy of this software and associated documentation files (the
   "Software"), to deal in the Software without restriction, including
   without limitation the rights to use, copy, modify, merge, publish,
   distribute, sublicense, and/or sell copies of the Software, and to
   permit persons to whom the Software is furnished to do so, subject to
   the following conditions:

   The above copyright notice and this permission notice shall be
   included in all copies or substantial portions of the Software.

   THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY
   KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE
   WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
   NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS
   BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN
   ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN
   CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
   SOFTWARE.
*/

/*
   Parser for a subset of CSP:

   Terminals:
      UCIDENT    Identifier that begins with an upper case letter
      LCIDENT    Identifier that begins with a lower case letter
      ALPHA      keyword "alpha"
      CHAOS      keyword "CHAOS"
      MU         keyword "mu"
      RUN        keyword "RUN"
      SKIP       keyword "SKIP"
      STOP       keyword "STOP"

   Productions:
      Unit = MainProcess Definitions .
      MainProcess = ProcessDefinition | ProcessExpression .
      Definitions = Definition |
	 Definitions Definition .
      Definition = FunctionDefinition | ProcessDefinition .
      FunctionDefinition = Identifier "(" Identifier ")" = Identifier .
      ProcessDefinition = Process "=" ProcessExpression |
	 Process Alphabet "=" ProcessExpression .
      ProcessExpression = ProcessSequence .
      ProcessSequence = ParallelProcesses |
	 ProcessSequence ";" ParallelProcesses .
      ParallelProcesses = InterleavingProcesses |
	 ParallelProcesses "||" InterleavingProcesses .
      InterleavingProcesses = ProcessChoices |
	 InterleavingProcesses "|||" ExternalChoice .
      ExternalChoice = InternalChoice |
	 ExternalChoice "[]" InternalChoice .
      InternalChoice = ConcealedProcessExpression |
	 InternalChoice = "|~|" ConcealedProcessExpression .
      ConcealedProcessExpression = LabeledProcessExpression |
	 LabeledProcessExpression "\" Alphabet .
      LabeledProcessExpression = SimpleProcessExpression |
	 Label COLON SimpleProcessExpression .
      SimpleProcessExpression = Process |
	 CHAOS Alphabet | CHAOS ALPHA Process |
	 RUN Alphabet | RUN ALPHA Process |
	 STOP Alphabet | STOP ALPHA Process |
	 SKIP Alphabet | SKIP ALPHA Process |
	 "(" ProcessExpression ")" |
	 "(" Choices ")" |
	 Identifier "(" ProcessExpression ")" |
	 MuHeader "(" Choices ")" .
      MuHeader = MU Process "." |
	 MU Process ":" Alphabet "." |
	 MU Process ":" ALPHA Process "." .
      Choices = PrefixExpression |
	 Choices "|" PrefixExpression .
      PrefixExpression = Identifier "->" PrefixExpression |
	 Event "->" ProcessExpression .
      Alphabet = "{" "}" | "{" AlphabetMembers "}" .
      AlphabetMembers = Event | AlphabetMembers "," Event .
      Event = Identifier .
      Label = Identifier .
      Identifier = LCIDENT | Identifier "." LCIDENT .
      Process = UCIDENT .
*/

%{
namespace CSP {
   class Scanner;
   class SymTable;
}

#include <memory>
#include "chaos-process.hpp"
#include "concealed-process.hpp"
#include "error.hpp"
#include "external-choice.hpp"
#include "event-set.hpp"
#include "identifier.hpp"
#include "interleaving-processes.hpp"
#include "internal-choice.hpp"
#include "mapped-process.hpp"
#include "object.hpp"
#include "parallel-processes.hpp"
#include "prefixed-process.hpp"
#include "process-definition.hpp"
#include "process-reference.hpp"
#include "process-sequence.hpp"
#include "process.hpp"
#include "recursive-process.hpp"
#include "run-process.hpp"
#include "scanner.hpp"
#include "selecting-process.hpp"
#include "skip-process.hpp"
#include "stop-process.hpp"
#include "symbol-changer.hpp"
#include "symtable.hpp"
#include "yytname.hpp"

using namespace CSP;

%}

%skeleton "lalr1.cc"
%define api.prefix {csp}
%define api.namespace { CSP }
%define api.value.type { CSP::ObjectPtr }
%defines
%verbose
%locations
%token-table
%parse-param { Scanner& scanner }
%parse-param { SymTable& symtab }
%parse-param { ProcessPtr& p }
%lex-param { Scanner& scanner }

/* operators */
%token ARROW OR EQUAL PARALLEL INTERLEAVES INTERNAL_CHOICE EXTERNAL_CHOICE
%token CONCEAL

/* delimiters */
%token LPAREN RPAREN LBRACE RBRACE COMMA SEMICOLON COLON PERIOD

/* identifiers */
%token LCIDENT UCIDENT

/* keywords */
%token ALPHA CHAOS RUN SKIP STOP MU

%%

start:
      { symtab.open(); }
   unit
      {
	 p = std::dynamic_pointer_cast<Process>($2);
	 if (!scanner.at_eof()) {
	    yyerror(&@$, "CSP program ends already here");
	 }
	 symtab.close();
      }
   ;

unit: main_process
   | main_process definitions
   ;

main_process: process_definition | process_expression
   ;

definitions: definition
      { $$ = $1; }
   | definitions definition
      { $$ = $1; } // just remember the very first definition
   ;

definition: function_definition | process_definition
   ;

function_definition: identifier LPAREN identifier RPAREN EQUAL identifier
      {
	 auto id = std::dynamic_pointer_cast<Identifier>($1);
	 auto event1 = std::dynamic_pointer_cast<Identifier>($3);
	 auto event2 = std::dynamic_pointer_cast<Identifier>($6);
	 auto function = symtab.lookup<FunctionDefinition>(id->get_name());
	 if (!function) {
	    function = std::make_shared<FunctionDefinition>(id->get_name());
	    assert(function);
	    bool ok = symtab.insert(id->get_name(), function);
	    assert(ok);
	 }
	 if (!function->add_mapping(event1, event2)) {
	    yyerror(&@$, "function definition violates injectivity");
	 }
	 $$ = function;
      }
   ;

process_definition: process EQUAL process_expression
      {
	 auto pn = std::dynamic_pointer_cast<ProcessReference>($1);
	 auto gp = std::dynamic_pointer_cast<Process>($3);
	 auto process = std::make_shared<ProcessDefinition>(pn->get_name(), gp);
	 bool ok = symtab.insert(pn->get_name(), process);
	 if (!ok) {
	    yyerror(&@$, "process defined multiple times");
	 }
	 $$ = process;
      }
   ;
process_definition: process alphabet EQUAL process_expression
      {
	 auto pn = std::dynamic_pointer_cast<ProcessReference>($1);
	 auto gp = std::dynamic_pointer_cast<Process>($4);
	 auto process = std::make_shared<ProcessDefinition>(pn->get_name(), gp);
	 auto evset = std::dynamic_pointer_cast<EventSet>($2);
	 process->set_alphabet(evset->get_alphabet());
	 bool ok = symtab.insert(pn->get_name(), process);
	 if (!ok) {
	    yyerror(&@$, "process defined multiple times");
	 }
	 $$ = process;
      }
   ;

process_expression: process_sequence
   ;

process_sequence: parallel_processes
   | process_sequence SEMICOLON parallel_processes
      {
	 auto p1 = std::dynamic_pointer_cast<Process>($1);
	 auto p2 = std::dynamic_pointer_cast<Process>($3);
	 $$ = std::make_shared<ProcessSequence>(p1, p2);
      }
   ;

parallel_processes: interleaving_processes
   | parallel_processes PARALLEL interleaving_processes
      {
	 auto p1 = std::dynamic_pointer_cast<Process>($1);
	 auto p2 = std::dynamic_pointer_cast<Process>($3);
	 $$ = std::make_shared<ParallelProcesses>(p1, p2);
      }
   ;

interleaving_processes: external_choice
   | interleaving_processes INTERLEAVES external_choice
      {
	 auto p1 = std::dynamic_pointer_cast<Process>($1);
	 auto p2 = std::dynamic_pointer_cast<Process>($3);
	 $$ = std::make_shared<InterleavingProcesses>(p1, p2);
      }
   ;

external_choice: internal_choice
   | external_choice EXTERNAL_CHOICE internal_choice
      {
	 auto p1 = std::dynamic_pointer_cast<Process>($1);
	 auto p2 = std::dynamic_pointer_cast<Process>($3);
	 $$ = std::make_shared<ExternalChoice>(p1, p2);
      }
   ;

internal_choice: concealed_process
   | internal_choice INTERNAL_CHOICE concealed_process
      {
	 auto p1 = std::dynamic_pointer_cast<Process>($1);
	 auto p2 = std::dynamic_pointer_cast<Process>($3);
	 $$ = std::make_shared<InternalChoice>(p1, p2);
      }
   ;

concealed_process: labeled_process_expression
   | labeled_process_expression CONCEAL alphabet
      {
	 auto p = std::dynamic_pointer_cast<Process>($1);
	 auto events = std::dynamic_pointer_cast<EventSet>($3);
	 auto concealed = events->get_alphabet();
	 if (concealed.cardinality() == 0) {
	    $$ = $1; // concealment operator with an empty set has no effect
	 } else {
	    $$ = std::make_shared<ConcealedProcess>(p, concealed);
	 }
      }
   ;

labeled_process_expression: simple_process_expression
   | label COLON simple_process_expression
      {
	 auto p = std::dynamic_pointer_cast<Process>($3);
	 auto label = std::dynamic_pointer_cast<Identifier>($1)->get_name();
	 auto changer = std::make_shared<Qualifier>(label);
	 $$ = std::make_shared<MappedProcess>(p, changer);
      }
   ;
simple_process_expression: process
   | CHAOS alphabet
      {
	 auto evset = std::dynamic_pointer_cast<EventSet>($2);
	 $$ = std::make_shared<ChaosProcess>(evset->get_alphabet());
      }
   | CHAOS ALPHA process
      {
	 auto alpha_p = std::dynamic_pointer_cast<Process>($3);
	 $$ = std::make_shared<ChaosProcess>(alpha_p);
      }
   | RUN alphabet
      {
	 auto evset = std::dynamic_pointer_cast<EventSet>($2);
	 $$ = std::make_shared<RunProcess>(evset->get_alphabet());
      }
   | RUN ALPHA process
      {
	 auto alpha_p = std::dynamic_pointer_cast<Process>($3);
	 $$ = std::make_shared<RunProcess>(alpha_p);
      }
   | SKIP alphabet
      {
	 auto evset = std::dynamic_pointer_cast<EventSet>($2);
	 $$ = std::make_shared<SkipProcess>(evset->get_alphabet());
      }
   | SKIP ALPHA process
      {
	 auto alpha_p = std::dynamic_pointer_cast<Process>($3);
	 $$ = std::make_shared<SkipProcess>(alpha_p);
      }
   | STOP alphabet
      {
	 auto evset = std::dynamic_pointer_cast<EventSet>($2);
	 $$ = std::make_shared<StopProcess>(evset->get_alphabet());
      }
   | STOP ALPHA process
      {
	 auto alpha_p = std::dynamic_pointer_cast<Process>($3);
	 $$ = std::make_shared<StopProcess>(alpha_p);
      }
   | LPAREN process_expression RPAREN
      { $$ = $2; }
   | LPAREN choices RPAREN
      { $$ = $2; }
   | identifier LPAREN process_expression RPAREN
      {
	 auto id = std::dynamic_pointer_cast<Identifier>($1);
	 auto p = std::dynamic_pointer_cast<Process>($3);
	 auto f = symtab.lookup<FunctionDefinition>(id->get_name());
	 if (!f) {
	    f = std::make_shared<FunctionDefinition>(id->get_name());
	    bool ok = symtab.insert(id->get_name(), f);
	    if (!ok) {
	       yyerror(&@$, "conflicting references");
	    }
	 }
	 $$ = std::make_shared<MappedProcess>(p, f);
      }
   | mu_header LPAREN choices RPAREN
      {
	 auto p = std::dynamic_pointer_cast<RecursiveProcess>($1);
	 auto pe = std::dynamic_pointer_cast<Process>($3);
	 p->set_process(pe);
	 symtab.close();
	 $$ = p;
      }
   ;

mu_header: MU process PERIOD
      {
	 auto pn = std::dynamic_pointer_cast<ProcessReference>($2);
	 auto p = std::make_shared<RecursiveProcess>(pn->get_name());
	 symtab.open();
	 bool ok = symtab.insert(pn->get_name(), p); assert(ok);
	 $$ = p;
      }
   | MU process COLON alphabet PERIOD
      {
	 auto pn = std::dynamic_pointer_cast<ProcessReference>($2);
	 auto evset = std::dynamic_pointer_cast<EventSet>($4);
	 auto p = std::make_shared<RecursiveProcess>(pn->get_name(),
	    evset->get_alphabet());
	 symtab.open();
	 bool ok = symtab.insert(pn->get_name(), p); assert(ok);
	 $$ = p;
      }
   | MU process COLON ALPHA process PERIOD
      {
	 auto pn = std::dynamic_pointer_cast<ProcessReference>($2);
	 auto alpha_p = std::dynamic_pointer_cast<ProcessReference>($5);
	 auto p = std::make_shared<RecursiveProcess>(pn->get_name(), alpha_p);
	 symtab.open();
	 bool ok = symtab.insert(pn->get_name(), p); assert(ok);
	 $$ = p;
      }
   ;

choices: prefix_expression
      {
	 auto p = std::dynamic_pointer_cast<Process>($1);
	 $$ = std::make_shared<SelectingProcess>(p);
      }
   | choices OR prefix_expression
      {
	 auto p1 = std::dynamic_pointer_cast<SelectingProcess>($1);
	 auto p2 = std::dynamic_pointer_cast<Process>($3);
	 auto intersection = p1->acceptable() * p2->acceptable();
	 if (intersection.cardinality() > 0) {
	    yyerror(&@$,
	       "prefix events within a choice must be unique");
	 }
	 p1->add_choice(p2);
	 $$ = p1;
      }
   ;

prefix_expression: event ARROW process_expression
      {  
	 auto event = std::dynamic_pointer_cast<Identifier>($1);
	 auto process = std::dynamic_pointer_cast<Process>($3);
	 $$ = std::make_shared<PrefixedProcess>(event->get_name(), process);
      }
   | event ARROW prefix_expression
      {  
	 auto event = std::dynamic_pointer_cast<Identifier>($1);
	 auto process = std::dynamic_pointer_cast<Process>($3);
	 $$ = std::make_shared<PrefixedProcess>(event->get_name(), process);
      }
   ;

alphabet: LBRACE RBRACE
      { $$ = std::make_shared<EventSet>(); }
   | LBRACE alphabet_members RBRACE
      { $$ = $2; }
   ;
alphabet_members: event
      {
	 auto event = std::dynamic_pointer_cast<Identifier>($1);
	 $$ = std::make_shared<EventSet>(event->get_name());
      }
   | alphabet_members COMMA event
      {
	 auto set = std::dynamic_pointer_cast<EventSet>($1);
	 auto event = std::dynamic_pointer_cast<Identifier>($3);
	 Alphabet a = set->get_alphabet();
	 a.add(event->get_name());
	 $$ = std::make_shared<EventSet>(a);
      }
   ;

event: identifier;
label: identifier;
identifier: LCIDENT
   | identifier PERIOD LCIDENT
      {
	 auto qualifier = std::dynamic_pointer_cast<Identifier>($1);
	 auto ident = std::dynamic_pointer_cast<Identifier>($3);
	 $$ = std::make_shared<Identifier>(qualifier, ident);
      }
   ;
process: UCIDENT;

%%
